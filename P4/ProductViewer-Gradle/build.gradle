plugins 
{
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

javafx {
    version = '11.0.2'
    modules = [ 'javafx.controls' ]
}

print("You are currently running Java version ${JavaVersion.current()}.\n")

if(!JavaVersion.current().isJava11Compatible())
{
    print "You need to be running Java 11 or above."
    throw new GradleException("Java 11 or above needed.")
}

mainClassName = 'ProductViewer'
sourceCompatibility = 11
defaultTasks 'run'

sourceSets 
{
    main
    {
        java { srcDirs = ['src'] }
        resources { srcDirs = ['resources'] }
    }
}

repositories
{
    mavenCentral()
}

jar
{
    manifest
    {
        attributes 'Main-Class': mainClassName
    }
}

def errorBuffer = new ByteArrayOutputStream()

task(checkRunErrors)
{
    doLast
    {
        print errorBuffer
        
        def error = errorBuffer.toString()
        if(error.indexOf('Unable to open DISPLAY') != -1)
        {
            print """
                Unable to open DISPLAY
                ----------------------
                This typically means you're running remotely (over SSH) and haven't enabled X11-
                forwarding.
                -> If using Linux, run 'ssh -Y ...'
                -> If using putty on Windows, you need to install an X server (such as Xming: 
                    http://www.straightrunning.com/XmingNotes/)
                """.stripIndent()
        }
    }
}

run
{
    finalizedBy(checkRunErrors)
    errorOutput(errorBuffer)
    standardInput(System.in)
}
